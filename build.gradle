plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'unq.desapp'
version = '0.0.1-SNAPSHOT'
description = 'Project for DesApp'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.13'
	implementation 'org.jsoup:jsoup:1.21.2'
	implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'net.logstash.logback:logstash-logback-encoder:8.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test:3.6.6'
    testImplementation 'org.wiremock:wiremock-standalone:3.13.2'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.0.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add '-parameters'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('unitTest', Test) {
	description = 'Runs only unit tests (fast tests with mocks, no external infrastructure)'
	group = 'verification'

	useJUnitPlatform {
		includeTags 'unit'
		excludeTags 'e2e'
		excludeTags 'arch'
	}

	shouldRunAfter test
}

tasks.register('e2eTest', Test) {
	description = 'Runs only E2E and integration tests (tests with databases, WireMock, etc.)'
	group = 'verification'

	useJUnitPlatform {
		includeTags 'e2e'
		excludeTags 'unit'
		excludeTags 'arch'
	}

	shouldRunAfter test, unitTest
}

tasks.register('archTest', Test) {
	description = 'Runs only architecture tests'
	group = 'verification'

	useJUnitPlatform {
		includeTags 'arch'
		excludeTags 'unit'
		excludeTags 'e2e'
	}

	shouldRunAfter test, unitTest, e2eTest
}

def jacocoExecutionFiles = [
	"build/jacoco/test.exec",
	"build/jacoco/unitTest.exec",
	"build/jacoco/e2eTest.exec"
]

def coverageExcludes = [
	'unq/desapp/futbol/FutbolProjectApplication.class',
	'unq/desapp/futbol/security/SecurityConfig*.class',
	'unq/desapp/futbol/constants/**'
]

jacoco {
	toolVersion = "0.8.8"
}

jacocoTestReport {
	executionData.from = files(jacocoExecutionFiles).filter { it.exists() }

	mustRunAfter test, unitTest, e2eTest

	reports {
		xml.required = true
		html.required = true
	}

	classDirectories.setFrom(sourceSets.main.output.asFileTree.matching {
		exclude coverageExcludes
	})
}

jacocoTestCoverageVerification {
	executionData.from = files(jacocoExecutionFiles).filter { it.exists() }

	mustRunAfter test, unitTest, e2eTest

	violationRules {
		rule {
			limit {
				minimum = 0.80
			}
		}
	}

	classDirectories.setFrom(sourceSets.main.output.asFileTree.matching {
		exclude coverageExcludes
	})
}
